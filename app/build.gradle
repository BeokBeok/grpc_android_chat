apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.osdetector'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.example.grpc_android"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            proto { srcDir './../idl' }
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xallow-result-return-type"]
    }

    buildFeatures {
        dataBinding = true
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.12.3"
    }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.30.1" }
        grpckt { artifact = "io.grpc:protoc-gen-grpc-kotlin:0.1.4" }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { option 'lite' }
                grpckt { option 'lite' }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // grpc
    implementation 'io.grpc:grpc-protobuf:1.30.2'
    implementation 'io.grpc:grpc-stub:1.30.2'
    implementation 'io.grpc:grpc-android:1.30.2'
    implementation 'io.grpc:grpc-okhttp:1.30.2'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation "io.grpc:grpc-kotlin-stub:0.1.4"

    // coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7'

    // conscrypt
    implementation 'org.conscrypt:conscrypt-android:2.4.0'

    // dagger
    implementation 'com.google.dagger:dagger-android:2.28.3'
    implementation 'com.google.dagger:dagger-android-support:2.28.3'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    kapt 'com.google.dagger:dagger-compiler:2.28.3'
    kapt 'com.google.dagger:dagger-android-processor:2.28.3'

    // ktx
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.activity:activity-ktx:1.1.0'

    // room
    implementation 'androidx.room:room-runtime:2.2.5'
    implementation "androidx.room:room-ktx:2.2.5"
    kapt 'androidx.room:room-compiler:2.2.5'

    // flipper
    debugImplementation "com.facebook.flipper:flipper:0.34.0"
    debugImplementation "com.facebook.soloader:soloader:0.8.2"

    // test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}